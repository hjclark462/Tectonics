// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel InitPlate
#pragma kernel JumpFill
#pragma kernel TestJFAColours
#pragma kernel SetPointData
#pragma kernel SetPointDataTS
#pragma kernel SmoothElevation
#pragma kernel SetHeightMap
#pragma kernel TestWorldColours
#pragma kernel TectonicShift
#pragma kernel GenerateHeatMap

struct PointData
{
	int2 position;	
	int area;
	int2 direction;
	int alive;
	int colliding;
	float mass;
	float thickness;
	float density;
	float height;	
	int plateNum;
};

RWTexture2D<float4> JFACalculation;
RWTexture2D<float4> JFAResult;
RWTexture2D<float4> PlateTracker;
RWTexture2D<float4> PlateResult;
RWTexture2D<float4> HeightMap;
RWTexture2D<float4> HeatMap;
RWStructuredBuffer<PointData> plates;
RWStructuredBuffer<PointData> points;
StructuredBuffer<float4> colours;
int maxHeight;
int minHeight;
int width;
int height;
int step;

// Clamps the flood fill so that it won't overflow the bounds of the RenderTexture
uint2 ClampToTexture(uint2 p)
{
	uint x = clamp(p.x, 0, width);
	uint y = clamp(p.y, 0, height);
	return uint2(x, y);
}

// Checks if the target point has a plate associated with it already and if it does whether its plate is closer
// is closer than the current one
void PointOnPlate(float2 current, float3 target, inout float4 minInfo)
{
	if (target.z > 0)
	{
		float distance = dot(current - target.xy, current - target.xy);
		if (distance < minInfo.w)
		{
			minInfo = float4(target, distance);
		}
	}
}

// Assign the generated plates to a co-ordinate on the texture for use in the JFA
// Texture XY is co-ords Z is plateNum ID + 1 so that 0 can be used as NULL
[numthreads(1, 1, 1)]
void InitPlate(uint3 id : SV_DispatchThreadID)
{
	int2 InitPlatePos = plates[id.x].position;
	uint px = uint(InitPlatePos.x);
	uint py = uint(InitPlatePos.y);
	JFACalculation[uint2(px, py)] = float4(px, py, id.x + 1, 1);
}

// Check the neighbours in 8 directions and the current point to determine the closest plateNum to attact the poin to
[numthreads(8, 8, 1)]
void JumpFill(uint3 id : SV_DispatchThreadID)
{
	float4 minInfo = float4(0, 0, 0, 999999);

	PointOnPlate(id.xy, JFACalculation[id.xy].xyz, minInfo);

	PointOnPlate(id.xy, JFACalculation[ClampToTexture(id.xy + uint2(-step, -step))].xyz, minInfo);

	PointOnPlate(id.xy, JFACalculation[ClampToTexture(id.xy + uint2(-step, step))].xyz, minInfo);

	PointOnPlate(id.xy, JFACalculation[ClampToTexture(id.xy + uint2(-step, 0))].xyz, minInfo);

	PointOnPlate(id.xy, JFACalculation[ClampToTexture(id.xy + uint2(0, -step))].xyz, minInfo);

	PointOnPlate(id.xy, JFACalculation[ClampToTexture(id.xy + uint2(0, step))].xyz, minInfo);

	PointOnPlate(id.xy, JFACalculation[ClampToTexture(id.xy + uint2(step, -step))].xyz, minInfo);

	PointOnPlate(id.xy, JFACalculation[ClampToTexture(id.xy + uint2(step, 0))].xyz, minInfo);

	PointOnPlate(id.xy, JFACalculation[ClampToTexture(id.xy + uint2(step, step))].xyz, minInfo);

	JFAResult[id.xy] = minInfo;
}

// Colour Debugging to be removed
[numthreads(8, 8, 1)]
void TestJFAColours(uint3 id : SV_DispatchThreadID)
{
	float4 info = JFACalculation[id.xy];
	if (info.w < 10)
	{
		// Render the seed that the flood fill generated from as a red dot
		JFAResult[id.xy] = float4(1, 0, 0, 1);
	}
	else
	{
		uint seedID = info.z;
		JFAResult[id.xy] = float4(colours[seedID - 1]);
	}
}

// Set the data from the JFA calculation to a CPU side array. 
[numthreads(8, 8, 1)]
void SetPointData(uint3 id : SV_DispatchThreadID)
{
	int pos = width * id.x + id.y;
	float4 info = JFACalculation[id.xy];
	points[pos] = plates[info.z];
	points[pos].position = id.xy;
	PlateTracker[id.xy] = float4(points[pos].position, points[pos].height, 1);
}

[numthreads(8, 8, 1)]
void SetPointDataTS(uint3 id : SV_DispatchThreadID)
{
	int pos = width * id.x + id.y;
	float4 info = PlateTracker[id.xy];
	points[pos].height = info.z;
}

// Find the average of the current position and its neighbours elevations to smooth the initial JFA calculation
// accounting for whether it is a border position or not
[numthreads(8, 8, 1)]
void SmoothElevation(uint3 id : SV_DispatchThreadID)
{
	int pos = width * id.x + id.y;
	float elevation;
	uint2 current = PlateTracker[id.xy].xy;

	if (current.x == 0)
	{
		// Top Left corner
		if (current.y == 0)
		{
			elevation = (PlateTracker[id.xy].z + PlateTracker[uint2(id.x + 1, id.y)].z +
				PlateTracker[uint2(id.x, id.y + 1)].z + PlateTracker[uint2(id.x + 1, id.y + 1)].z) / 4;
		}
		// Bottom Left corner
		else if (current.y == uint(height - 1))
		{
			elevation = (PlateTracker[uint2(id.x, id.y - 1)].z + PlateTracker[uint2(id.x + 1, id.y - 1)].z +
				PlateTracker[id.xy].z + PlateTracker[uint2(id.x + 1, id.y)].z) / 4;
		}
		// Left Edge
		else
		{
			elevation = (PlateTracker[uint2(id.x, id.y - 1)].z + PlateTracker[uint2(id.x + 1, id.y - 1)].z +
				PlateTracker[id.xy].z + PlateTracker[uint2(id.x + 1, id.y)].z +
				PlateTracker[uint2(id.x, id.y + 1)].z + PlateTracker[uint2(id.x + 1, id.y + 1)].z) / 6;
		}
	}
	else if (current.y == 0)
	{
		// Top Right Corner
		if (current.x == uint(width - 1))
		{
			elevation = (PlateTracker[uint2(id.x - 1, id.y)].z + PlateTracker[id.xy].z +
				PlateTracker[uint2(id.x - 1, id.y + 1)].z + PlateTracker[uint2(id.x, id.y + 1)].z) / 4;
		}
		// Top Edge
		else
		{
			elevation = (PlateTracker[uint2(id.x - 1, id.y)].z + PlateTracker[id.xy].z + PlateTracker[uint2(id.x + 1, id.y)].z +
				PlateTracker[uint2(id.x - 1, id.y + 1)].z + PlateTracker[uint2(id.x, id.y + 1)].z + PlateTracker[uint2(id.x + 1, id.y + 1)].z) / 6;
		}
	}
	else if (current.x == uint(width - 1))
	{
		// Bottom Right Corner
		if (current.y == uint(height - 1))
		{
			elevation = (PlateTracker[uint2(id.x - 1, id.y - 1)].z + PlateTracker[uint2(id.x, id.y - 1)].z +
				PlateTracker[uint2(id.x - 1, id.y)].z + PlateTracker[id.xy].z) / 4;
		}
		// Right Edge
		else
		{
			elevation = (PlateTracker[uint2(id.x - 1, id.y - 1)].z + PlateTracker[uint2(id.x, id.y - 1)].z +
				PlateTracker[uint2(id.x - 1, id.y)].z + PlateTracker[id.xy].z +
				PlateTracker[uint2(id.x - 1, id.y + 1)].z + PlateTracker[uint2(id.x, id.y + 1)].z) / 6;
		}
	}
	// Bottom Edge
	else if (current.y == uint(height - 1))
	{
		elevation = (PlateTracker[uint2(id.x - 1, id.y - 1)].z + PlateTracker[uint2(id.x, id.y - 1)].z + PlateTracker[uint2(id.x + 1, id.y - 1)].z +
			PlateTracker[uint2(id.x - 1, id.y)].z + PlateTracker[id.xy].z + PlateTracker[uint2(id.x + 1, id.y)].z) / 6;
	}
	// Central Position
	else
	{
		elevation = (PlateTracker[uint2(id.x - 1, id.y - 1)].z + PlateTracker[uint2(id.x, id.y - 1)].z + PlateTracker[uint2(id.x + 1, id.y - 1)].z +
			PlateTracker[uint2(id.x - 1, id.y)].z + PlateTracker[id.xy].z + PlateTracker[uint2(id.x + 1, id.y)].z +
			PlateTracker[uint2(id.x - 1, id.y + 1)].z + PlateTracker[uint2(id.x, id.y + 1)].z + PlateTracker[uint2(id.x + 1, id.y + 1)].z) / 9;
	}
	points[pos].height = elevation;
	PlateResult[id.xy] = float4(points[pos].position, points[pos].height, 1);
}

// Set texture colours to a shade of white to generate a height map
[numthreads(8, 8, 1)]
void SetHeightMap(uint3 id : SV_DispatchThreadID)
{
	float elevation = PlateTracker[id.xy].z;
	HeightMap[id.xy] = float4(elevation, elevation, elevation, 1);
}

// Set texture colours to a shade of white to generate a height map
[numthreads(8, 8, 1)]
void TestWorldColours(uint3 id : SV_DispatchThreadID)
{
	float elevation = PlateTracker[id.xy].z;

	if (elevation >= 0.95)
	{
		PlateResult[id.yx] = float4(elevation, elevation, elevation, 1);
	}
	else if (elevation >= 0.75 && elevation < 0.95)
	{
		PlateResult[id.yx] = float4(1- elevation, 1- elevation, 1- elevation, 1);
	}
	else if (elevation >= 0.52 && elevation < 0.75)
	{
		PlateResult[id.yx] = float4(0, 1- elevation, 0, 1);
	}
	else if (elevation >= 0.5 && elevation < 0.52)
	{
		PlateResult[id.yx] = float4(1, 1, elevation, 1);
	}
	else if (elevation < 0.5)
	{
		PlateResult[id.yx] = float4(0, 0, elevation, 1);
	}
}

bool OutOfBounds(int2 p, int2 d)
{
	int2 coord = (p + d);
		if (coord.x < 0 || coord.x > width || coord.y < 0 || coord.y > height)
		{
			return true;
		}
		else {
			return false;
		}
}

[numthreads(8, 8, 1)]
void GenerateHeatMap(uint3 id : SV_DispatchThreadID)
{
	float4 info = JFACalculation[id.xy];
	float intensity = 1 - exp(-sqrt(info.w) * 0.01);
	HeatMap[id.xy] = float4(intensity, intensity, intensity, 1);
}

void Buoyancy(int i)
{
	points[i].density = points[i].mass / (points[i].area * points[i].thickness);
	points[i].height = points[i].thickness * (1 - points[i].density);
}

float Greyscale(float value)
{
	return (0.299f * value) + (0.587f * value) + (0.114f * value);
}

[numthreads(8, 8, 1)]
void TectonicShift(uint3 id : SV_DispatchThreadID)
{
	int pos = width * id.x + id.y;
	int x1 = id.x + points[pos].direction.x;
	int y1 = id.y + points[pos].direction.y;
	int pos1 = width * x1 + y1;
	if (OutOfBounds(points[pos].position, points[pos].direction))
	{
		points[pos].alive = 0;
		return;
	}
	else
	{
		int2 dir = points[pos].direction;
		int2 dir1 = points[pos1].direction;
		float difference = (points[pos1].height - points[pos].height) * Greyscale(HeatMap[uint2(x1, y1)]);
		if (points[pos].plateNum == points[pos1].plateNum)
		{
			return;
		}
		else
		{
			float scale = dot(dir, dir1) / normalize(dir1);			
			PlateTracker[uint2(x1, y1)] = float4(points[pos1].position, points[pos].height + (difference * scale), 1);			
		}
	}
}

